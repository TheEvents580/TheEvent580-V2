plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta7'
}

group = 'fr.thefox580'
version = '1.7.6'

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = "Noxcrew Public Maven Repository"
        url = "https://maven.noxcrew.com/public"
    }
    maven {
        name = "neonRepositoryReleases"
        url = "https://repo.superneon4ik.me/releases"
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly "com.github.ZockerAxel:CrazyAdvancementsAPI:v2.1.21"

    implementation "me.superneon4ik:NoxesiumUtils:2.0.0"
    implementation "com.noxcrew.noxesium:api:2.5.0"
    implementation 'org.mongodb:mongodb-driver-sync:5.5.1'
    implementation 'org.mongodb:bson:5.5.1'

    //runtimeOnly 'org.mongodb:mongodb-driver-reactivestreams:4.5.1'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set('TheEvent580 V2')
    archiveClassifier.set('') // Ensure no classifier is added to the name
    relocate 'com.mongodb', 'fr.thefox580.theevent5802.libs.mongodb'
}

tasks.build {
    dependsOn shadowJar
}

tasks.jar {
    enabled = false
}